# Skip heavy shell init for AI agents (Cursor, Claude Code)
if [[ -n "$CURSOR_AGENT" || -n "$CLAUDE_CODE" ]]; then
  PS1='%~ %# '
  SKIP_HEAVY_SHELL_INIT=1
fi

# Homebrew (needed early for fnm and other brew packages)
eval "$(/opt/homebrew/bin/brew shellenv)"

# fnm (Fast Node Manager) - fast, no lazy loading needed
# --version-file-strategy=recursive: looks up parent dirs for .node-version/.nvmrc
# --log-level=quiet: suppress output
eval "$(fnm env --use-on-cd --version-file-strategy=recursive --log-level=quiet)"

# Spaceship prompt
if [[ -z "$SKIP_HEAVY_SHELL_INIT" ]]; then
  [[ -f ~/.config/spaceship.zsh ]] && source ~/.config/spaceship.zsh
  source "$(brew --prefix)/opt/spaceship/spaceship.zsh"
fi

# Environment variables
export github="$HOME/github"

# Aliases - Personal
alias nano="code"
alias cat="bat"
alias ls="eza --icons"
alias ff="fzf --preview 'bat --color=always --style=numbers --line-range=:500 {}'"
alias date="gdate"
alias pn="pnpm"
alias vim=nvim
alias vi=nvim
alias ws=worktree-status
alias bathelp='bat --plain --language=help'

# Tmux aliases
alias t='tmux'
alias ta='tmux attach'
alias tls='tmux list-sessions'
alias tn='tmux new-session -s'
alias tka='tmux kill-server'
alias td='tmux detach'

# Interactive session attach with fzf
tat() {
  local session
  session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --height 40% --reverse --prompt="attach> ")
  [[ -n "$session" ]] && tmux attach -t "$session"
}

# Interactive session kill with fzf
tk() {
  local session
  session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --height 40% --reverse --prompt="kill> ")
  [[ -n "$session" ]] && tmux kill-session -t "$session"
}

# Tmux coding layout - creates session with 4 panes (claude, nvim, 2 terminals)
# Usage: coding [directory]
# Example: coding ~/instacart/isc-web
coding() {
  local dir
  if [[ -z "$1" ]]; then
    dir="$(pwd)"
  elif [[ -d "$1" ]]; then
    dir="$(cd "$1" && pwd)"
  else
    dir="$(zoxide query "$1" 2>/dev/null)" || { echo "No match found for: $1"; return 1; }
  fi
  local name="${dir##*/}"

  if [[ -n "$TMUX" ]]; then
    echo "Already in tmux. Detach first or run from outside tmux."
    return 1
  fi

  # If session exists, just attach to it
  if tmux has-session -t "$name" 2>/dev/null; then
    tmux attach-session -t "$name"
    return 0
  fi

  tmux new-session -d -s "$name" -c "$dir" \; \
    rename-window "$name" \; \
    split-window -h -c "$dir" \; \
    split-window -v -c "$dir" \; \
    select-pane -t 1 \; \
    split-window -v -c "$dir" \; \
    select-layout "c288,200x52,0,0{100x52,0,0[100x44,0,0,40,100x7,0,45,43],99x52,101,0[99x44,101,0,41,99x7,101,45,42]}" \; \
    select-pane -t 1 \; \
    send-keys 'claude' C-m \; \
    select-pane -t 3 \; \
    send-keys 'nvim' C-m \; \
    select-pane -t 1 \; \
    attach-session -t "$name"
}

# Git aliases (formerly from Oh My Zsh git plugin)
alias g='git'
alias ga='git add'
alias gaa='git add --all'
alias gc='git commit'
alias gcm='git commit -m'
alias gco='git checkout'
alias gd='git diff'
alias gst='git status'
alias gp='git push'
alias gl='git pull'
alias glog='git log --oneline --graph --decorate'

# Critique - diff viewer
alias cw='critique --watch'           # watch unstaged changes (for Claude Code)
alias cws='critique --watch --staged' # watch staged changes
alias cr='critique'                   # quick alias
alias cm='critique $(git merge-base origin/master HEAD)'  # PR-style diff (from fork point)

# Delta - watch for changes (uses fswatch for efficiency)
gwatch() {
  echo "Watching for changes... (Ctrl+C to exit)"
  git diff
  fswatch -o . 2>/dev/null | while read; do
    clear
    git diff
  done
}
alias dw='gwatch'

export BAT_THEME="Catppuccin Macchiato"

batdiff() {
    git diff --name-only --relative --diff-filter=d | xargs bat --diff
}

help() {
    "$@" --help 2>&1 | bathelp
}

export GPG_TTY=$(tty)

# Default editors
export EDITOR="nvim"
export VISUAL="nvim"
export GIT_EDITOR="nvim"

export PATH="/usr/local/bin:$PATH"
export PATH="$HOME/go/bin:$PATH"
export PATH="$HOME/bin:$PATH"

# Shell integrations (skip in agent mode)
if [[ -z "$SKIP_HEAVY_SHELL_INIT" ]]; then
  [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
fi

export PGHOST=localhost

# Rust
[[ -f "$HOME/.cargo/env" ]] && source "$HOME/.cargo/env"

export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"

# Completions (skip in agent mode)
if [[ -z "$SKIP_HEAVY_SHELL_INIT" ]]; then
  # bun completions
  [ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"
fi

function cursor {
  open -a "/Applications/Cursor.app" "$@"
}

[[ -f "$HOME/.local/bin/env" ]] && . "$HOME/.local/bin/env"

# Custom terminal title: "dir|process"
function set_terminal_title() {
  local dir="${PWD##*/}"
  [[ -z "$dir" ]] && dir="~"
  print -Pn "\e]0;${dir}|zsh\a"
}

function set_terminal_title_with_cmd() {
  local dir="${PWD##*/}"
  [[ -z "$dir" ]] && dir="~"
  local cmd="${1[(w)1]}"
  print -Pn "\e]0;${dir}|${cmd}\a"
}

precmd_functions+=(set_terminal_title)
preexec_functions+=(set_terminal_title_with_cmd)

# History substring search (handles multiline prompts correctly)
source /opt/homebrew/share/zsh-history-substring-search/zsh-history-substring-search.zsh
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# Cached zoxide init (regenerate with: zoxide init zsh > ~/.cache/zoxide.zsh)
[[ -f ~/.cache/zoxide.zsh ]] && source ~/.cache/zoxide.zsh

# Source local/work-specific config (not in dotfiles repo)
[[ -f ~/.zsh/local.zsh ]] && source ~/.zsh/local.zsh
